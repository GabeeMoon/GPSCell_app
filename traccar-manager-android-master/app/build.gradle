plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

android {
    compileSdk 34
    defaultConfig {
        applicationId 'org.gpscell.manager'
        minSdkVersion 19
        targetSdkVersion 34
        versionCode 37
        versionName '4.1'
        multiDexEnabled true
    }
    namespace 'org.gpscell.manager'

    buildFeatures {
        flavorDimensions = ['default']
    }
    productFlavors {
        regular {
            isDefault = true
            ext.enableCrashlytics = false
        }
        google
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }
    dependenciesInfo {
        includeInApk true
        includeInBundle true
    }
    buildToolsVersion '34.0.0'
    ndkVersion '25.1.8937393'
}

dependencies {
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.1.0'
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.8.2'
    implementation 'androidx.core:core-ktx:1.13.1'
    implementation 'androidx.biometric:biometric-ktx:1.2.0-alpha05'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.google.firebase:firebase-messaging:24.0.0'
    googleImplementation platform('com.google.firebase:firebase-bom:33.1.0')
    googleImplementation 'com.google.firebase:firebase-core'
    googleImplementation 'com.google.firebase:firebase-analytics:22.0.1'
    googleImplementation 'com.google.firebase:firebase-crashlytics:19.0.1'
    googleImplementation 'com.google.firebase:firebase-messaging:24.0.0'
}
tasks.register('copyFirebaseConfig', Copy) {
    from '../../environment/google-services'
    into '.'
    include 'traccar-manager-android.json'
    rename('traccar-manager-android.json', 'google-services.json')
}
afterEvaluate {
    tasks.matching { it.name.contains('Google') }.configureEach { task ->
        if (task.name.contains('Regular')) {
            task.enabled false
        } else {
            task.dependsOn copyFirebaseConfig
        }
    }
}
